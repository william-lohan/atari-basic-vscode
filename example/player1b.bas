50 REM **** INIT ML STRING
55 GOSUB 7500
100 REM LIST "D:PLAYER1B.LST"
110 REM 
120 REM 
130 REM *****************************
140 REM * SIMPE PLAYER MISSLE INTRO *
150 REM 
160 VPOSP1=120:HPOSP1=120:HEIGHTP1=6
170 REM **** SET PM BASE
180 LET NEWTOP=PEEK(106)-8
190 POKE (106),NEWTOP
200 GRAPHICS 3+16
205 GOSUB 8500
210 LET PB=NEWTOP*256
220 POKE 54279,NEWTOP
230 REM 
240 REM **** SINGLE LINE RESOLUTION
250 POKE 559,62
260 REM 
290 REM 
300 REM **** GIVE PLAYERS PRIORITY
310 POKE 623,8
320 REM **** ENABLE PLYR MISLE GRAPHI
330 POKE 53277,3
340 REM 
350 REM **** SET COLOR PLAYER 0
360 POKE 704,84
370 REM 
380 REM **** SET X POS FOR PLAYER 0
390 POKE 53248,0
400 REM 
410 REM **** SET WIDTH OF PLAYER 0
420 REM **** 0=NORMAL,1=DOUBLE
430 REM **** 3=QUADRUPLE
440 POKE 53256,0
450 REM 
460 REM **** CLEAR PLAYER 0 MEM
470 P1=PB+1024
480 X=USR(ADR(MEMCLR$),P1,256)
490 REM 
500 REM **** FILL PLAYER DATA
510 FOR Y=P1+VPOSP1 TO P1+VPOSP1+HEIGHTP1-1
520 READ Z
530 POKE Y,Z
540 NEXT Y
550 POKE 53248,HPOSP1
630 REM ******* MAIN LOOP ********
640 STICK1=STICK(0)
650 IF STICK1=14 THEN GOSUB 730
660 IF STICK1=13 THEN GOSUB 845
670 IF STICK1=11 THEN GOSUB 960
680 IF STICK1=7 THEN GOSUB 1010
690 GOTO 640
700 REM **************************
710 REM 
720 REM 
730 REM ******* MOVE PLAYER UP ML
740 X=USR(ADR(MVPLUP$),P1+VPOSP1,HEIGHTP1)
760 VPOSP1=VPOSP1-1
770 RETURN 
771 REM 
780 REM ******* MOVE PLAYER UP BASIC
800 FOR NVPOSP1=VPOSP1-1 TO VPOSP1+HEIGHTP1
810 POKE P1+NVPOSP1,PEEK(P1+NVPOSP1+1)
820 NEXT NVPOSP1
830 VPOSP1=VPOSP1-1
840 RETURN 
845 REM ******* MOVE PLAYER DOWN ML
847 X=USR(ADR(MVPLDOWN$),P1+VPOSP1,HEIGHTP1)
860 VPOSP1=VPOSP1+1
870 RETURN 
890 REM ******* MOVE PLAYER DOWN BASIC
900 FOR NVPOSP1=VPOSP1+HEIGHTP1 TO VPOSP1 STEP -1
910 POKE P1+NVPOSP1,PEEK(P1+NVPOSP1-1)
920 NEXT NVPOSP1
930 VPOSP1=VPOSP1+1
940 RETURN 
950 REM ***************************
960 REM MOVE PLAYER LEFT
970 HPOSP1=HPOSP1-1
980 POKE 53248,HPOSP1
990 RETURN 
1000 REM ***************************
1010 REM MOVE PLAYER RIGHT
1020 HPOSP1=HPOSP1+1
1030 POKE 53248,HPOSP1
1040 RETURN 
7500 REM ***** ML CLEAR PM MEM
7510 REM ***** CALL USR(ADR(MEMCLR$),PB*256,2048) FOR SINGLE LINE RES
7520 REM 
7530 DIM MEMCLR$(36):MEMCLR$="hh…Ìh…Ëhª©� ÿà�Ðh¨©�‘ËˆÀÿÐùæÌÊàÿÐê`"
7540 REM 
7550 REM 
7560 REM ***** ML TO MOVE PLAYER UP
7570 REM ***** CALL USR(ADR(MVPLUP$),P1+VPOSP1-1,HEIGHTP1)
7580 REM 
7590 DIM MVPLUP$(44):MVPLUP$="hh…Ìh…Ëh…Îh…Í8¥Ëé…Ë¥Ìé�…Ì ±Ëˆ‘ËÄÍÈÈõ©�‘Ë`"
7600 REM 
7610 REM 
7611 REM ***** ML TO MOVE PLAYER DOWN
7612 REM ***** CALL USR(ADR(MVPLDOWN$),P1+VPOSP1-1,HEIGHTP1)
7613 REM 
7614 DIM MVPLDOWN$(31):MVPLDOWN$="hh…Ìh…Ëh…Îh…Í¤Í±ËÈ‘Ëˆˆ÷©� �‘Ë`"
7615 REM 
7616 REM 
7620 RETURN 
8000 REM ************DATA**********
8001 REM 
8090 DATA 24,60,60,126,255,60
8500 REM ***************************
8505 REM DRAW BACKGROUND
8510 REM 
8520 START=0:N=1
8530 SETCOLOR 4,0,0
8535 FOR STRIP=1 TO 6
8540 COLOR N:START=START+5
8545 FOR I=START TO START+3
8550 PLOT I,0:DRAWTO I,19
8555 NEXT I
8560 N=N+1:IF N=4 THEN N=1
8565 NEXT STRIP
8570 RETURN 